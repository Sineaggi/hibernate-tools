plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'org.hibernate.tool'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
     mavenCentral()
     mavenLocal()
}

dependencies {
    implementation "org.hibernate.tool:hibernate-tools-orm:$version"
}

gradlePlugin {
    plugins {
        hibernate {
            id = 'org.hibernate.tool.hibernate-tools-gradle'
            implementationClass = 'org.hibernate.tool.gradle.Plugin'
        }
    }
}

testing {
    suites {
        named('test') {
            useJUnitJupiter(libs.versions.junit.jupiter)
        }

        functionalTest(JvmTestSuite) {
            testType = TestSuiteType.FUNCTIONAL_TEST
            useJUnitJupiter(libs.versions.junit.jupiter)
            dependencies {
                implementation project()
                implementation gradleTestKit()
            }

            targets {
                configureEach {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

sourceSets.functionalTest {
    java {
        resources.srcDir(tasks.pluginUnderTestMetadata)
    }
}

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}
